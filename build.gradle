buildscript {
    ext.ibm_mq_version = '9.0.4.0'
    ext.kotlin_version = '1.2.40'
    ext.junit_version = '4.12'
    ext.jackson_version = '2.9.5'
    ext.jedis_version = '2.9.0'
    ext.nav_person_v3_version = '3.2.0'
    ext.jaxb2_plugin_version = '1.0.7'
    ext.redis_mock_version = '0.1.6'
    ext.logback_version = '1.2.3'
    ext.kafka_version = '1.0.0'
    ext.confluent_version = '4.0.0'
    ext.$mustache_version = '0.9.5'
    ext.jaxb2SourceDir = file("$buildDir/generated-sources/jaxb2")
    ext.avro_version = '1.8.2'
    ext.cxf_version = '3.2.4'
    ext.nav_journalbehandling = '1.3.3'
    ext.nav_tssws_organisasjon_v3 = '1.0'
    ext.rxkotlin = '2.2.0'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name "JCenter Gradle Plugins"
            url  "https://dl.bintray.com/gradle/gradle-plugins"
        }

        maven {
            name "Gradle plugin portal"
            url "https://plugins.gradle.org/m2/"
        }

    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.0"
    }
}

group 'no.nav.legeerklaering'
version '1.0-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "com.commercehub.gradle.plugin.avro"

configurations {
    generateJaxb2
}

task generateJaxb2() {
    doLast {
        print("$projectDir/src/main/resources/binding.xjb")
        jaxb2SourceDir.deleteDir()
        jaxb2SourceDir.mkdirs()

        ant.taskdef(
                classpath: configurations.generateJaxb2.asPath,
                name: 'xjc',
                classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
        )

        ant.xjc(
                destdir: jaxb2SourceDir,
                package: 'no.nav.model.legeerklaering',
                schema: "$projectDir/src/main/resources/xsd/NAV-Legeerklaring-2008-06-06.xsd",
                encoding: 'UTF-8',
                fork: "true"
        )

        ant.xjc(
                destdir: jaxb2SourceDir,
                schema: "$projectDir/src/main/resources/xsd/Fellesformat_1.0.xsd",
                encoding: 'UTF-8',
                fork: "true"
        ) {
            binding(dir: "$projectDir/src/main/xjb", includes: "fellesformat_apprec.xjb")
        }

        ant.xjc(
                destdir: jaxb2SourceDir,
                package: 'no.nav.model.arenainfo',
                schema: "$projectDir/src/main/resources/xsd/ArenaInfo2-1.xsd",
                encoding: 'UTF-8',
                fork: "true"
        )
    }
}

sourceSets {
    main {
        java {
            srcDir jaxb2SourceDir
        }
    }
}

compileKotlin {
    dependsOn generateJaxb2
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.adeo.no/repository/maven-releases/'
    }
    maven {
        url 'https://repo.adeo.no/repository/maven-snapshots/'
    }

    maven {
        name 'Confluent maven repo'
        url 'http://packages.confluent.io/maven/'
    }
}

dependencies {
    generateJaxb2 "com.sun.xml.bind:jaxb-xjc:2.3.0"
    generateJaxb2 "com.sun.xml.bind:jaxb-impl:2.3.0"
    generateJaxb2 "com.sun.xml.bind:jaxb-core:2.3.0"
    generateJaxb2 "org.jvnet.jaxb2_commons:jaxb2-basics-ant:1.11.1"

    compile "no.nav.tjenester:nav-person-v3-tjenestespesifikasjon:$nav_person_v3_version"
    compile "com.ibm.mq:com.ibm.mq.allclient:$ibm_mq_version"
    compile "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jackson_version"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "redis.clients:jedis:$jedis_version"
    compile "ai.grakn:redis-mock:$redis_mock_version"
    compile "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    compile "org.apache.cxf:cxf-rt-features-logging:$cxf_version"

    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.confluent:kafka-avro-serializer:$confluent_version"
    implementation "org.apache.kafka:kafka-clients:$kafka_version"
    implementation "no.nav.tjenester:nav-journalbehandling-tjenestespesifikasjon:$nav_journalbehandling:jaxws"
    implementation "no.nav.tjeneste.tssws:nav-fim-tssws-organisasjon-v3-tjenestespesifikasjon:$nav_tssws_organisasjon_v3:jaxws"

    testImplementation "junit:junit:$junit_version"
}
