buildscript {
    ext.artemis_version = '2.6.2'
    ext.commons_text_version = "1.4"
    ext.coroutines_version = '0.23.4'
    ext.cxf_version = '3.2.5'
    ext.ibm_mq_version = '9.0.5.0'
    ext.io_prometheus_version = '0.4.0'
    ext.jackson_version = '2.9.6'
    ext.jaxb_basic_ant_version = '1.11.1'
    ext.jaxb_version = '2.3.0.1'
    ext.jedis_version = '2.9.0'
    ext.jetty_version = '9.4.11.v20180605'
    ext.junit_version = '4.12'
    ext.kotlin_version = '1.2.51'
    ext.ktor_version = '0.9.3'
    ext.logback_version = '1.2.3'
    ext.logstash_encoder_version = '5.1'
    ext.mockito_version = "2.19.1"
    ext.mustache_version = '0.9.5'
    ext.nav_person_v3_version = '3.2.0'
    ext.nav_journalbehandling = '1.3.3'
    ext.nav_organisasjonEnhet_v2 = '2.1.0'
    ext.nav_tssws_organisasjon_v3 = '1.0'
    ext.okhttp_version = '3.11.0'
    ext.redis_mock_version = '0.1.6'
    ext.rxkotlin = '2.2.0'

    ext.jaxb2SourceDir = file("$buildDir/generated-sources/jaxb2")
}

plugins {
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.jetbrains.kotlin.jvm' version "1.2.51"
    id 'application'
    id 'java'
    id 'idea'
    id 'org.jmailen.kotlinter' version '1.14.0'
}


group 'no.nav.pale'
version '1.0-SNAPSHOT'


mainClassName = 'no.nav.pale.PaleApplicationKt'

applicationDefaultJvmArgs = [
        '-Dcom.ibm.mq.cfg.useIBMCipherMappings=false',
        '-Djavax.net.ssl.trustStore=${NAV_TRUSTSTORE_PATH}',
        '-Djavax.net.ssl.trustStorePassword=${NAV_TRUSTSTORE_PASSWORD}',
        '-Djavax.net.ssl.trustStoreType=JKS',
        '-XX:+UnlockExperimentalVMOptions',
        '-XX:+UseCGroupMemoryLimitForHeap',
        '-XX:+UseG1GC',
        '-XshowSettings:vm'/*,
        '-Dlogback.configurationFile=logback-remote.xml'*/
]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PATH}', '${NAV_TRUSTSTORE_PATH}')
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PASSWORD}', '${NAV_TRUSTSTORE_PASSWORD}')
    }
}

configurations {
    generateJaxb2
}

test {
    useJUnitPlatform {
        includeEngines 'spek2', 'junit-vintage'
    }
    testLogging {
        showStandardStreams = true
    }
}

task printVersion() {
    doLast {
        println(version)
    }
}

class Jaxb2Task extends DefaultTask {
    @OutputDirectory
    File outputDir
    @InputDirectory
    File sourceDir
    @InputDirectory
    File xjbDir

    @TaskAction
    def generateFiles() {
        outputDir.deleteDir()
        outputDir.mkdirs()
        ant.taskdef(
                classpath: project.configurations.generateJaxb2.asPath,
                name: 'xjc',
                classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
        )

        ant.xjc(
                destdir: outputDir,
                package: 'no.nav.model.pale',
                schema: "$sourceDir/NAV-Legeerklaring-2008-06-06.xsd",
                encoding: 'UTF-8',
                fork: "true"
        )

        ant.xjc(
                destdir: outputDir,
                schema: "$sourceDir/Fellesformat_1.0.xsd",
                encoding: 'UTF-8',
                fork: "true"
        ) {
            binding(dir: xjbDir, includes: "fellesformat_apprec.xjb")
        }

        ant.xjc(
                destdir: outputDir,
                package: 'no.nav.model.arenainfo',
                schema: "$sourceDir/ArenaInfo2-1.xsd",
                encoding: 'UTF-8',
                fork: "true"
        )
    }
}

task generateJaxb2(type: Jaxb2Task) {
    outputDir = jaxb2SourceDir
    sourceDir = file("$projectDir/src/main/resources/xsd")
    xjbDir = file("$projectDir/src/main/xjb")
}

sourceSets {
    main {
        java {
            srcDir jaxb2SourceDir
        }
    }
}

compileKotlin {
    dependsOn generateJaxb2
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url 'https://repo.adeo.no/repository/maven-releases/' }
    maven { url 'https://repo.adeo.no/repository/maven-snapshots/' }
    maven { url  "https://dl.bintray.com/spekframework/spek-dev" }
}

dependencies {
    generateJaxb2 "com.sun.xml.bind:jaxb-xjc:$jaxb_version"
    generateJaxb2 "com.sun.xml.bind:jaxb-impl:$jaxb_version"
    generateJaxb2 "com.sun.xml.bind:jaxb-core:$jaxb_version"
    generateJaxb2 "org.jvnet.jaxb2_commons:jaxb2-basics-ant:$jaxb_basic_ant_version"

    // General application flow
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Communication libraries
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-features-logging:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-transports-http:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-ws-security:$cxf_version"

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "redis.clients:jedis:$jedis_version"
    implementation "com.ibm.mq:com.ibm.mq.allclient:$ibm_mq_version"

    // Everything used for HTTP stats and selftest
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.prometheus:simpleclient_hotspot:$io_prometheus_version"
    implementation "io.prometheus:simpleclient_common:$io_prometheus_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"

    implementation "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    implementation "no.nav.tjenester:nav-person-v3-tjenestespesifikasjon:$nav_person_v3_version"
    implementation "no.nav.tjenester:nav-journalbehandling-tjenestespesifikasjon:$nav_journalbehandling:jaxws"
    implementation "no.nav.tjenester:nav-organisasjonEnhet-v2-tjenestespesifikasjon:$nav_organisasjonEnhet_v2:jaxws"

    implementation "org.apache.commons:commons-text:$commons_text_version"

    testImplementation "ai.grakn:redis-mock:$redis_mock_version"
    testImplementation "com.devskiller:jfairy:0.6.2"
    testImplementation "org.apache.activemq:artemis-server:$artemis_version"
    testImplementation "org.apache.activemq:artemis-jms-client:$artemis_version"

    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.eclipse.jetty:jetty-servlet:$jetty_version"
    testImplementation 'org.spekframework.spek2:spek-dsl-jvm:2.0.0-alpha.1'
    testImplementation "org.junit.vintage:junit-vintage-engine:5.2.0"
    testRuntimeOnly "org.spekframework.spek2:spek-runtime-jvm:2.0.0-alpha.1"
    testRuntimeOnly 'org.spekframework.spek2:spek-runner-junit5:2.0.0-alpha.1'
}
