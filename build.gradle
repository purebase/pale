buildscript {
    ext.ibm_mq_version = '9.0.5.0'
    ext.kotlin_version = '1.2.40'
    ext.junit_version = '4.12'
    ext.jackson_version = '2.9.5'
    ext.jedis_version = '2.9.0'
    ext.nav_person_v3_version = '3.2.0'
    ext.jaxb2_plugin_version = '1.0.7'
    ext.redis_mock_version = '0.1.6'
    ext.logback_version = '1.2.3'
    ext.kafka_version = '1.0.0'
    ext.confluent_version = '4.0.0'
    ext.$mustache_version = '0.9.5'
    ext.jaxb2SourceDir = file("$buildDir/generated-sources/jaxb2")
    ext.avro_version = '1.8.2'
    ext.cxf_version = '3.2.4'
    ext.nav_journalbehandling = '1.3.3'
    ext.nav_tssws_organisasjon_v3 = '1.0'
    ext.rxkotlin = '2.2.0'
    ext.nav_organisasjonEnhet_v2 = '2.1.0'
    ext.ktor_version = '0.9.2'
    ext.logstash_encoder_version = '5.1'
    ext.io_prometheus_version = '0.4.0'
    ext.commons_text_version = "1.3"


    repositories {
        mavenCentral()
        jcenter()
        maven {
            name "JCenter Gradle Plugins"
            url  "https://dl.bintray.com/gradle/gradle-plugins"
        }

        maven {
            name "Gradle plugin portal"
            url "https://plugins.gradle.org/m2/"
        }

    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.0"
    }
}

group 'no.nav.pale'
version '1.0-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "com.commercehub.gradle.plugin.avro"
apply plugin: 'application'

mainClassName = 'no.nav.pale.PaleApplicationKt'

applicationDefaultJvmArgs = [
        '-Dcom.ibm.mq.cfg.useIBMCipherMappings=false',
        '-Djavax.net.ssl.trustStore=${NAV_TRUSTSTORE_PATH}',
        '-Djavax.net.ssl.trustStorePassword=${NAV_TRUSTSTORE_PASSWORD}',
        '-Djavax.net.ssl.trustStoreType=JKS',
        '-XX:+UnlockExperimentalVMOptions',
        '-XX:+UseCGroupMemoryLimitForHeap',
        '-XX:+UseG1GC',
        '-XshowSettings:vm'/*,
        '-Dlogback.configurationFile=logback-remote.xml'*/
]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PATH}', '${NAV_TRUSTSTORE_PATH}')
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PASSWORD}', '${NAV_TRUSTSTORE_PASSWORD}')
    }
}

configurations {
    generateJaxb2
}

avro {
    fieldVisibility = "PRIVATE"
}

task printVersion() {
    doLast {
        println(version)
    }
}

task generateJaxb2() {
    doLast {
        print("$projectDir/src/main/resources/binding.xjb")
        jaxb2SourceDir.deleteDir()
        jaxb2SourceDir.mkdirs()

        ant.taskdef(
                classpath: configurations.generateJaxb2.asPath,
                name: 'xjc',
                classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
        )

        ant.xjc(
                destdir: jaxb2SourceDir,
                package: 'no.nav.model.pale',
                schema: "$projectDir/src/main/resources/xsd/NAV-Legeerklaring-2008-06-06.xsd",
                encoding: 'UTF-8',
                fork: "true"
        )

        ant.xjc(
                destdir: jaxb2SourceDir,
                schema: "$projectDir/src/main/resources/xsd/Fellesformat_1.0.xsd",
                encoding: 'UTF-8',
                fork: "true"
        ) {
            binding(dir: "$projectDir/src/main/xjb", includes: "fellesformat_apprec.xjb")
        }

        ant.xjc(
                destdir: jaxb2SourceDir,
                package: 'no.nav.model.arenainfo',
                schema: "$projectDir/src/main/resources/xsd/ArenaInfo2-1.xsd",
                encoding: 'UTF-8',
                fork: "true"
        )
    }
}

sourceSets {
    main {
        java {
            srcDir jaxb2SourceDir
        }
    }
}

compileKotlin {
    dependsOn generateJaxb2
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven {
        url 'https://repo.adeo.no/repository/maven-releases/'
    }
    maven {
        url 'https://repo.adeo.no/repository/maven-snapshots/'
    }

    maven {
        name 'Confluent maven repo'
        url 'http://packages.confluent.io/maven/'
    }
}

dependencies {
    generateJaxb2 "com.sun.xml.bind:jaxb-xjc:2.3.0"
    generateJaxb2 "com.sun.xml.bind:jaxb-impl:2.3.0"
    generateJaxb2 "com.sun.xml.bind:jaxb-core:2.3.0"
    generateJaxb2 "org.jvnet.jaxb2_commons:jaxb2-basics-ant:1.11.1"

    // General application flow
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin"

    // Communication libraries
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-features-logging:$cxf_version"
    compile "org.apache.cxf:cxf-rt-ws-security:$cxf_version"

    implementation "com.squareup.okhttp3:okhttp:3.10.0"
    implementation "redis.clients:jedis:$jedis_version"
    implementation "io.confluent:kafka-avro-serializer:$confluent_version"
    implementation "org.apache.kafka:kafka-clients:$kafka_version"
    implementation "com.ibm.mq:com.ibm.mq.allclient:$ibm_mq_version"

    // Everything used for HTTP stats and selftest
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5"
    implementation "io.prometheus:simpleclient_hotspot:$io_prometheus_version"
    implementation "io.prometheus:simpleclient_common:$io_prometheus_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"

    implementation "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    implementation "no.nav.tjenester:nav-person-v3-tjenestespesifikasjon:$nav_person_v3_version"
    implementation "no.nav.tjenester:nav-journalbehandling-tjenestespesifikasjon:$nav_journalbehandling:jaxws"
    implementation "no.nav.tjenester:nav-organisasjonEnhet-v2-tjenestespesifikasjon:$nav_organisasjonEnhet_v2:jaxws"

    implementation "org.apache.commons:commons-text:$commons_text_version"

    testImplementation "ai.grakn:redis-mock:$redis_mock_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "com.devskiller:jfairy:0.6.2"
    testImplementation "org.apache.activemq:artemis-server:2.5.0"
    testImplementation "org.apache.activemq:artemis-jms-client:2.5.0"

    testImplementation 'org.mockito:mockito-core:2.18.3'
    testImplementation 'org.eclipse.jetty:jetty-servlet:9.4.10.v20180503'
    testImplementation "org.apache.cxf:cxf-rt-transports-http:$cxf_version"
}
